group 'com.transistorsoft.flutter.backgroundfetch'
version '1.0-SNAPSHOT'

def DEFAULT_LIFE_CYCLE_RUNTIME_VERSION          = "2.7.0"
def DEFAULT_LIFE_CYCLE_EXTENSIONS_VERSION       = "2.2.0"

buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url './libs'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    if (project.android.hasProperty("namespace")) {
        namespace("com.transistorsoft.flutter.backgroundfetch")
    }

    compileSdkVersion safeExtGet('compileSdkVersion', 28)

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    def lifeCycleRuntimeVersion = safeExtGet('lifeCycleRuntimeVersion', DEFAULT_LIFE_CYCLE_RUNTIME_VERSION)
    def lifeCycleExtensionsVersion = safeExtGet('lifeCycleExtensionsVersion', DEFAULT_LIFE_CYCLE_EXTENSIONS_VERSION)

    implementation(group: 'com.transistorsoft', name:'tsbackgroundfetch', version: '+')

    // LifeCycleObserver
    implementation "androidx.lifecycle:lifecycle-runtime:$lifeCycleRuntimeVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifeCycleExtensionsVersion"
}
